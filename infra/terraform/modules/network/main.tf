resource "azurerm_virtual_network" "this" {
  name                = var.name
  resource_group_name = var.resource_group_name
  location            = var.location
  address_space       = var.address_space
  tags                = var.tags
}

resource "azurerm_subnet" "this" {
  for_each             = var.subnets
  name                 = each.value.name
  resource_group_name  = var.resource_group_name
  virtual_network_name = azurerm_virtual_network.this.name
  address_prefixes     = each.value.address_prefixes

  dynamic "delegation" {
    for_each = each.value.service_delegation == null ? [] : [each.value.service_delegation]
    content {
      name = "delegation"

      service_delegation {
        name = delegation.value.name
        actions = coalesce(
          delegation.value.actions,
          ["Microsoft.Network/virtualNetworks/subnets/action"]
        )
      }
    }
  }
}
